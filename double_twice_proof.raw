
;; define the + function "(+ n m) increments 'm' n times" 
(claim base-+
 (-> Nat Nat))
(define base-+
 (lambda (m) m))

(claim step-+
 (-> Nat Nat))
(define step-+
 (lambda (k) (add1 k)))

(claim +
 (-> Nat Nat Nat))
(define +
 (lambda (n m)
  (iter-Nat n
   (base-+ m)
   step-+)))

(claim double
 (-> Nat Nat))
(define double
  (lambda (n) (+ n n)))

(claim twice
 (-> Nat Nat))
(define twice
  (lambda (n)
    (iter-Nat n
              zero
              (+ 2))))

;; (double 17)
;; (twice 17)

;; note that we are inducting over the second
;; argument 'm'
(claim mot-add1+=+add1
 (-> Nat Nat U))
(define mot-add1+=+add1
 (lambda (n m)
     (= Nat (add1 (+ m n)) (+ m (add1 n)))))

(claim base-add1+=+add1
  (Pi ([n Nat])
    (= Nat (add1 (+ 0 n)) (+ 0 (add1 n)))))
(define base-add1+=+add1
  (lambda (n)
    (same (add1 n))))

(claim step-add1+=+add1
  (Pi ([n Nat] [m Nat])
      (-> (= Nat (add1 (+       m  n)) (+       m  (add1 n)))
          (= Nat (add1 (+ (add1 m) n)) (+ (add1 m) (add1 n))))))
(define step-add1+=+add1
  (lambda (n m)
    (lambda (casem)
     (cong casem (+ 1)))))

(claim add1+=+add1
 (Pi ([n Nat] [m Nat])
     (= Nat (add1 (+ m n)) (+ m (add1 n)))))
(define add1+=+add1
  (lambda (n m)
    (ind-Nat m
      (mot-add1+=+add1 n)
      (base-add1+=+add1 n)
      (step-add1+=+add1 n))))

(claim mot-double=twice
 (Pi ([n Nat]) U))
(define mot-double=twice
 (lambda (n) (= Nat (double n) (twice n))))

;; easy to prove.  both definitions fall into their base case
(claim base-double=twice
 (= Nat (double 0) (twice 0)))
(define base-double=twice
 (same zero))

;; step needs to return a (= Nat (add1 (+ n (add1 n))) (add1 (add1 (twice n))))
;;               same as  (cong (= Nat (+ n (add1 n)) (add1 (twice n))) (+ 1))
;;               same as  (cong (= Nat (add1 (+ n n)) (add1 (twice n))) (+ 1))
(claim step-double=twice
 (Pi ([n Nat])
     (-> (= Nat (double       n ) (twice       n ))
         (= Nat (double (add1 n)) (twice (add1 n))))))
(define step-double=twice
  (lambda (n)
    (lambda (almost) ;; this has type (= Nat (+ n n) (twice n))
       (cong (trans (symm (add1+=+add1 n n)) (cong almost (+ 1))) (+ 1)))))

(claim double=twice
 (Pi ([n Nat])
  (= Nat (double n) (twice n))))
(define double=twice
  (lambda (n)
    (ind-Nat n
             mot-double=twice
             base-double=twice
             step-double=twice)))

